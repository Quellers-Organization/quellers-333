import org.elasticsearch.gradle.Version
import org.elasticsearch.gradle.VersionProperties
import org.elasticsearch.gradle.util.GradleUtils
import org.elasticsearch.gradle.internal.info.BuildParams
import org.elasticsearch.gradle.testclusters.StandaloneRestIntegTestTask

apply plugin: 'elasticsearch.internal-java-rest-test'
apply plugin: 'elasticsearch.internal-test-artifact-base'
apply plugin: 'elasticsearch.bwc-test'

dependencies {
  testImplementation project(path: ':x-pack:plugin:inference:qa:inference-service-tests')
  compileOnly project(':x-pack:plugin:core')
  javaRestTestImplementation(testArtifact(project(xpackModule('core'))))
  javaRestTestImplementation project(path: xpackModule('inference'))
  clusterPlugins project(
    ':x-pack:plugin:inference:qa:test-service-plugin'
  )
}

// Inference API added in 8.11, but we generate BwC as far back as 8.9.0 to test for mixed clusters
// with nodes where index descriptors were non-existing/used the older Version-based versioning
def supportedVersion = bwcVersion -> {
  return bwcVersion.onOrAfter(Version.fromString("8.9.0"));
}

BuildParams.bwcVersions.withWireCompatible(supportedVersion) { bwcVersion, baseName ->
  def javaRestTest = tasks.register("v${bwcVersion}#javaRestTest", StandaloneRestIntegTestTask) {
    usesBwcDistribution(bwcVersion)
    systemProperty("tests.old_cluster_version", bwcVersion)
    maxParallelForks = 1
  }

  tasks.register(bwcTaskName(bwcVersion)) {
    dependsOn javaRestTest
  }
}

