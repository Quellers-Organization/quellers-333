apply plugin: 'elasticsearch.internal-es-plugin'
apply plugin: 'elasticsearch.internal-java-rest-test'

esplugin {
  name 'x-pack-stack'
  description 'Elasticsearch Expanded Pack Plugin - Stack'
  classname 'org.elasticsearch.xpack.stack.StackPlugin'
  extendedPlugins = ['x-pack-core']
  hasNativeController false
  requiresKeystore true
}

base {
  archivesName = 'x-pack-stack'
}

dependencies {
  compileOnly project(path: xpackModule('core'))
  testImplementation project(':modules:data-streams')
  javaRestTestImplementation(testArtifact(project(xpackModule('core'))))
  javaRestTestImplementation project(path: ':x-pack:plugin:stack')
  clusterModules project(':modules:mapper-extras')
  clusterModules project(xpackModule('wildcard'))
}

// These tests are only invoked direclty as part of a dedicated build job
tasks.named('javaRestTest').configure {task ->
  def taskNames = gradle.startParameter.getTaskNames()
  def currentDir = gradle.startParameter.currentDir
  def projectDir = project.projectDir

  onlyIf("E2E test task must be invoked directly") {
    taskNames.contains(task.path) ||
        (taskNames.contains(task.name) && currentDir == projectDir)
    }
}

addQaCheckDependencies(project)
